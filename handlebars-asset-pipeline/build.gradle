buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'idea'

group = 'com.bertramlabs.plugins'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
sourceCompatibility = '1.17'
targetCompatibility = '1.17'

java {
    withSourcesJar()
    withJavadocJar()
}

repositories {
	mavenLocal()
    mavenCentral()
    mavenCentral()

}

configurations {
    provided
}
 
sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

dependencies {

	api project(':asset-pipeline-core')

    // Needs to be compiled with Groovy 3 as it is used by the Gradle Plugin
    compileOnly "org.codehaus.groovy:groovy:$groovy3Version"
    compileOnly "org.codehaus.groovy:groovy-templates:$groovy3Version"
    compileOnly "org.graalvm.polyglot:polyglot:$graalvmVersion"

    testImplementation "org.apache.groovy:groovy:$groovy4Version"
    testImplementation "org.graalvm.js:js-community:$graalvmVersion"
    testImplementation "org.spockframework:spock-core:$spockVersion"

    testRuntimeOnly "org.apache.groovy:groovy-templates:$groovy4Version"
    testRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId 'handlebars-asset-pipeline'
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'handlebars-asset-pipeline'
                    description 'Handlebars.js Transpiler Extension for the JVM Based Asset-Pipeline.'
                    url 'https://github.com/bertramdev/handlebars-asset-pipeline'
                    scm {
                        url 'https://github.com/bertramdev/handlebars-asset-pipeline'
                        connection 'scm:https://bertramdev@github.com/bertramdev/handlebars-asset-pipeline.git'
                        developerConnection 'scm:git://github.com/bertramdev/handlebars-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'davydotcom'
                            name 'David Estes'
                            email 'davydotcom@gmail.com'
                        }
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}


task(console, dependsOn: 'classes', type: JavaExec) {
  main = 'groovy.ui.Console'
  classpath = sourceSets.main.runtimeClasspath
}

test {
    useJUnitPlatform()
    testLogging {
        events('passed', 'skipped', 'failed')
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}
