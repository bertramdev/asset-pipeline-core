buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
	}
}



apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'idea'

group = 'com.bertramlabs.plugins'
sourceCompatibility = '1.17'
targetCompatibility = '1.17'
repositories {
	mavenCentral()
}

configurations {
	provided
	doc.extendsFrom(runtime)
}

sourceSets {
	main {
		compileClasspath += configurations.provided
		groovy.srcDirs   += ['src/main/java']
		java.srcDirs      = []
	}
}

java {
    withSourcesJar()
    withJavadocJar()
}

dependencies {

	api project(':asset-pipeline-core')

	implementation "org.apache.groovy:groovy:$groovy4Version"

	compileOnlyApi "jakarta.servlet:jakarta.servlet-api:$servletApiVersion"

	testImplementation "org.apache.httpcomponents:fluent-hc:$fluentHcVersion"
	testImplementation "org.apache.httpcomponents:httpcore:$httpcoreVersion"
	testImplementation "junit:junit:$junit4Version"
	testImplementation "org.eclipse.jetty:jetty-server:$jettyVersion"
	testImplementation "org.eclipse.jetty:jetty-servlet:$jettyVersion"
	testImplementation "org.eclipse.jetty:jetty-webapp:$jettyVersion"

	testRuntimeOnly "org.slf4j:slf4j-simple:$slf4jVersion"
}

test {
	testLogging {
		events('passed', 'skipped', 'failed')
		exceptionFormat = 'full'
		showStandardStreams = true
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			artifactId 'asset-pipeline-servlet'
			pom.withXml {
				asNode().children().last() + {
					resolveStrategy = Closure.DELEGATE_FIRST
					name 'asset-pipeline-servlet'
					description 'JVM Asset Pipeline Servlet Adapter.'
					url 'https://github.com/bertramdev/asset-pipeline-core'
					scm {
						url 'https://github.com/bertramdev/asset-pipeline-core'
						connection 'scm:https://bertramdev@github.com/bertramdev/asset-pipeline-core.git'
						developerConnection 'scm:git://github.com/bertramdev/asset-pipeline-core.git'
					}
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/license/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
					developers {
						developer {
							id 'davydotcom'
							name 'David Estes'
							email 'davydotcom@gmail.com'
						}
					}
				}
			}
			from components.java
		}
	}

    repositories {
        maven {
            if( !isReleaseVersion ) {
                url = "http://nexus.bertramlabs.com/content/repositories/snapshots"
                if(project.hasProperty('labsNexusUser')) {
                    credentials {
                        username = labsNexusUser
                        password = labsNexusPassword
                    }
                }
            }
        }
    }
}


task(console, dependsOn: 'classes', type: JavaExec) {
	main = 'groovy.ui.Console'
	classpath = sourceSets.main.runtimeClasspath
}
